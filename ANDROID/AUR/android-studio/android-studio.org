* android-studio/PKGBUILD
  #+BEGIN_SRC sh :tangle android-studio/PKGBUILD :padline no :noweb yes
    # Maintainer:  danyf90 <daniele.formichelli@gmail.com>
    # Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
    # Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
    # Contributor: Christoph Brill <egore911-at-gmail-dot-com>
    # Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

    pkgname=android-studio
    pkgver=1.1.0
    pkgrel=1
    _build=135.1740770
    pkgdesc="A new Android development environment based on IntelliJ IDEA."
    arch=('i686' 'x86_64')
    url="http://developer.android.com/sdk/installing/studio.html"
    license=('APACHE')
    depends=('java-environment' 'python' 'ttf-font')
    makedepends=('unzip')
    optdepends=('android-sdk' 'android-sdk-platform-tools' 'android-sdk-build-tools')
    conflicts=('android-studio-beta' 'android-studio-dev' 'android-studio-canary')
    options=('!strip')
    install=$pkgname.install
    source=("https://dl.google.com/dl/android/studio/ide-zips/$pkgver/android-studio-ide-$_build-linux.zip"
            "$pkgname.desktop")
    sha512sums=('6c333b30ea9dfa6eae40871401af6c64e34cd4a77e943a1c89cb3edc984e7c6a5895e3600cb936b8ca75d0223aa773f2f5c1983c6066125bc1567353552d1f19'
                'fa3567f328af45d265c4fabf3e41b55d8e8cccfa9675e745f07dd6ae41950dd53a2ef41ef1caee86643f5c2ddf7a7681ee17155e208a7b6fdae6c0537dfc0c94')

    if [[ $CARCH == "x86_64" ]]; then
      depends+=('lib32-fontconfig' 'lib32-libxrender' 'lib32-mesa')
    else
      depends+=('fontconfig' 'libxrender' 'mesa')
    fi

    prepare() {
      cd $srcdir/$pkgname

      # extract the application icon
      unzip -qo lib/resources.jar artwork/icon_AS_128.png

      # enable anti aliasing
      echo "-Dswing.aatext=true" >> studio.vmoptions
      echo "-Dswing.aatext=true" >> studio64.vmoptions
    }

    package() {
      cd $srcdir/$pkgname

      # application stuff
      install -d $pkgdir/{opt/$pkgname,usr/bin}
      cp -a bin lib plugins $pkgdir/opt/$pkgname
      ln -s /opt/android-studio/bin/studio.sh $pkgdir/usr/bin/android-studio

      # starter stuff
      install -Dm644 artwork/icon_AS_128.png $pkgdir/usr/share/pixmaps/$pkgname.png
      install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

      chmod -R ugo+rX $pkgdir/opt
    }

  #+END_SRC
* android-studio/android-studio.install
  #+BEGIN_SRC sh :tangle android-studio/android-studio.install :padline no 
    post_install() {
        update-desktop-database -q
    }

    post_upgrade() {
        update-desktop-database -q
    }

    post_remove() {
        update-desktop-database -q
    }
  #+END_SRC
* android-studio/android-studio.desktop
  #+BEGIN_SRC sh :tangle android-studio/android-studio.desktop :padline no
    [Desktop Entry]
    Version=1.0
    Type=Application
    Name=Android Studio
    Exec=android-studio %f
    Icon=android-studio
    Comment=Develop with pleasure!
    Categories=Development;IDE;
    Terminal=false
    StartupNotify=true
    StartupWMClass=jetbrains-android-studio
    MimeType=application/x-extension-iml;

  #+END_SRC
